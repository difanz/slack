#!/usr/bin/perl -w
# $Id$
# vim:sw=2
# vim600:fdm=marker

# This script is in charge of copying files from the (possibly remote)
# master directory to a local cache, using rsync

require 5.006;
use warnings FATAL => qw(all);
use strict;
use sigtrap qw(die untrapped normal-signals
               stack-trace any error-signals);

use File::Path;
use File::Find;
use POSIX; # for strftime

use constant DEFAULT_CONFIG_FILE => '/etc/slack.conf';
use constant LIBEXEC_DIR => '/usr/lib/slack';

(my $PROG = $0) =~ s#.*/##;

# Arguments to pass to each backends (initialized to a hash of empty arrays)
my %backend_flags = ( map { $_ => [] }
  qw(getroles sync stage preinstall fixfiles installfiles postinstall)
);

my @roles;

sub run_command (@);

########################################
# Environment
# Helpful prefix to die messages
$SIG{__DIE__} = sub { die "FATAL[$PROG]: @_"; };
# Set a reasonable umask
umask 077;
# Get out of wherever (possibly NFS-mounted) we were
chdir("/")
  or die "Could not chdir /: $!";
# Autoflush on STDERR
select((select(STDERR), $|=1)[0]);

########################################
# Config and option parsing {{{
my $usage = <<EOF;
Usage: $PROG [options] [<role>...]

Options:
  -h, -?, --help
      Print this help message and exit.

  -v, --verbose
      Be verbose

  -C, --config  FILE
      Use this config file instead of /etc/slack.conf

  -s, --source  DIR
      Source for slack files

  -c, --cache  DIR
      Local cache directory for slack files

  -t, --stage  DIR
      Local staging directory for slack files

  -r, --root  DIR
      Root destination for slack files

  --no-files
      Don't install any files in ROOT, but tell rsync to print what
      it would do.

  --no-scripts
      Don't run scripts

  -n, --dry-run
      Same as --no-files --no-scripts  (CACHE, STAGE will still be updated)

  --role-list
      Role list for slack-getroles

  -b, --backup
      Make backups of existing files in ROOT that are overwritten

  --backup-dir  DIR
      Put backups into this directory.
EOF

# Options
my %opt = (
  verbose => 0,
  backup => 1,
);
# A special option to shut off verbose
$opt{quiet} = sub { $opt{verbose} = 0; };
# So we can distinguish stuff on the command line from config file stuff
my %command_line_opt = ();

# Reads through the config file
sub read_config () {
  my $config = $opt{config};
  open(CONFIG, "<", $config)
    or die "Could not open config file '$config': $!";
  while(<CONFIG>) {
    chomp;
    s/#.*//; # delete comments
    s/\s+$//; # delete trailing spaces
    next if m/^$/; # skip empty lines
    if (m/^[A-Z_]+=\S+/) {
      my ($key, $value) = split(/=/, $_, 2);
      $key =~ tr/A-Z_/a-z-/;
      if (not defined $opt{$key}) {
        ($opt{verbose} > 2) and print STDERR "$PROG: Setting '$key' to '$value'\n";
        $opt{$key} = $value;
      }
    } else {
      warn "WARNING[$PROG]: Garbage line '$_' in '$config' line $.\n";
    }
  }
  close(CONFIG)
    or die "Could not close config file: $!";
}

# Collect options
{
    use Getopt::Long;
    Getopt::Long::Configure ("bundling");
    GetOptions(\%opt,
      'verbose|v+',
      'quiet',
      'help|h|?',
      'backup|b',
      'backup-dir=s',
      'config|C=s',
      'role-list=s',
      'no-scripts',
      'no-files',
      'dry-run|n',
      'source|s=s',
      'cache|c=s',
      'stage|t=s',
      'root|r=s',
      ) or die $usage;
    if ($opt{help}) {
        print $usage;
        exit 0;
    }
    # Remember command line options
    %command_line_opt = %opt;
    # Set a default config file
    if (not defined $opt{config}) {
      $opt{config} = DEFAULT_CONFIG_FILE;
    }
    read_config;
}

# Some conf variables are required
die "No source defined!\n\n$usage" unless $opt{source};
die "No root defined!\n\n$usage" unless $opt{root};
die "No cache defined!\n\n$usage" unless $opt{cache};

# Special options
if ($opt{'dry-run'}) {
  $opt{'no-scripts'} = 1;
  $opt{'no-files'} = 1;
}
if ($opt{'no-scripts'}) {
  for my $action (qw(fixfiles preinstall postinstall)) {
    push @{$backend_flags{$action}},
      '--dry-run';
  }
}
if ($opt{'no-files'}) {
  push @{$backend_flags{installfiles}},
    '--dry-run';
}
# propagate verbosity - 1 to all backends
if ($command_line_opt{'verbose'} > 1) {
  for my $action (keys %backend_flags) {
    push @{$backend_flags{$action}},
      ('--verbose') x ($command_line_opt{'verbose'} - 1);
  }
}
# propagate these flags to all the backends
for my $option (qw(config root cache stage source)) {
  if ($command_line_opt{$option}) {
    for my $action (keys %backend_flags) {
      push @{$backend_flags{$action}},
        "--$option=$command_line_opt{$option}";
    }
  }
}
# getroles also can take 'role-list'
if ($command_line_opt{'role-list'}) {
  push @{$backend_flags{'getroles'}},
    "--role-list=$command_line_opt{'role-list'}";
}
    
# Figure out a place to put backups
if ($opt{backup} and $opt{'backup-dir'}) {
  push @{$backend_flags{installfiles}},
    '--backup',
    '--backup-dir='.
      $opt{'backup-dir'}.
      "/".
      strftime('%F-%T', localtime(time))
    ;
}
# }}}

# Get a list of roles to install from slack-getroles {{{
if (not @ARGV) {
  my @command = (LIBEXEC_DIR.'/slack-getroles', @{$backend_flags{'getroles'}});
  $opt{verbose} and print STDERR "$PROG: getroles\n";
  ($opt{verbose} > 2) and print STDERR "$PROG: Calling '@command' to get a list of roles for this host.\n";
  my ($roles_pid, $roles_fh);
  if ($roles_pid = open($roles_fh, "-|")) {
    # Parent
  } elsif (defined $roles_pid) {
    # Child
    exec(@command);
    die "Could not exec '@command': $!\n";
  } else {
    die "Could not fork to run '@command': $!\n";
  }
  @roles = split(/\s+/, join(" ", <$roles_fh>));
  unless (close($roles_fh)) {
    if ($!) {
        die "Syserr on system '@command': $!\n";
    }
    if ($? & 128) {
        die "'@command' dumped core\n";
    }
    if (my $sig = $? & 127) {
        die "'@command' caught sig $sig\n";
    }
    if (my $exit = $? >> 8) {
        die "'@command' returned $exit\n";
    }
    die "Unknown error on '@command'\n";
  }
} else {
  @roles = @ARGV;
}
# }}}

# Check role name syntax {{{
for my $role (@roles) {
  # Roles MUST begin with a letter.  All else is reserved.
  if ($role !~ m/^[a-zA-Z]/) {
    die "Role '$role' does not begin with a letter!";
  }
}
# }}}

$opt{verbose} and print STDERR "$PROG: installing roles: @roles\n";

# sync all the roles down at once
$opt{verbose} and print STDERR "$PROG: sync @roles\n";
run_command(LIBEXEC_DIR.'/slack-sync',
  @{$backend_flags{sync}}, @roles);

for my $role (@roles) {
  # stage
  $opt{verbose} and print STDERR "$PROG: stage $role\n";
  run_command(LIBEXEC_DIR.'/slack-stage',
    @{$backend_flags{stage}}, $role);

  # preinstall
  $opt{verbose} and print STDERR "$PROG: preinstall $role\n";
  run_command(LIBEXEC_DIR.'/slack-runscript',
    @{$backend_flags{preinstall}}, 'preinstall', $role);

  # fixfiles
  $opt{verbose} and print STDERR "$PROG: fixfiles $role\n";
  run_command(LIBEXEC_DIR.'/slack-runscript',
    @{$backend_flags{fixfiles}}, 'fixfiles', $role);
  # installfiles
  $opt{verbose} and print STDERR "$PROG: install $role\n";
  run_command(LIBEXEC_DIR.'/slack-installfiles',
    @{$backend_flags{installfiles}}, $role);

  # postinstall
  $opt{verbose} and print STDERR "$PROG: postinstall $role\n";
  run_command(LIBEXEC_DIR.'/slack-runscript',
    @{$backend_flags{postinstall}}, 'postinstall', $role);
}
exit 0;

sub run_command (@) {
  my (@command) = @_;
  ($opt{verbose} > 2) and print STDERR "$PROG: Calling '@command'\n";
  unless (system(@command) == 0) {
    if ($? & 128) {
        die "'@command' dumped core\n";
    }
    if (my $sig = $? & 127) {
        die "'@command' caught sig $sig\n";
    }
    if (my $exit = $? >> 8) {
        die "'@command' returned $exit\n";
    }
    if ($!) {
        die "Syserr on system '@command': $!\n";
    }
    die "Unknown error on '@command'\n";
  }
}
